model {
    # Priors and constraints
    for (t in 1:(n.occasions-1)){
        phi.intro1[t] <- mean.phi1.hab1
        phi.native1[t] <- mean.phi1.hab2
        phi.intro2[t] <- mean.phi2.hab1
        phi.native2[t] <- mean.phi2.hab2
        p.intro[t] <- mean.p.hab1
        p.native[t] <- mean.p.hab2
    }

    mean.phi1.hab1 ~ dunif(0, 1)          # Prior for mean survival in introduced habitat
    mean.phi1.hab2 ~ dunif(0, 1)           # Prior for mean survival in native habitat
    mean.phi2.hab1 ~ dunif(0, 1)          # Prior for mean survival in introduced habitat
    mean.phi2.hab2 ~ dunif(0, 1)           # Prior for mean survival in native habitat
    mean.p.hab1 ~ dunif(0, 1)               # Prior for mean recapture
    mean.p.hab2 ~ dunif(0, 1)
    # Define the multinomial likelihood
    for (t in 1:(n.occasions-1)){
        marr.i1[t,1:n.occasions] ~ dmulti(pr.i1[t,], r.i1[t])
        marr.n1[t,1:n.occasions] ~ dmulti(pr.n1[t,], r.n1[t])
        marr.i2[t,1:n.occasions] ~ dmulti(pr.i2[t,], r.i2[t])
        marr.n2[t,1:n.occasions] ~ dmulti(pr.n2[t,], r.n2[t])
    }
    
    # Define the cell probabilities of the m-arrays
    # Main diagonal
    for (t in 1:(n.occasions-1)){
        q.intro[t] <- 1-p.intro[t]            # Probability of non-recapture
        q.native[t] <- 1-p.native[t]
        pr.i1[t,t] <- phi.intro1[t]*p.intro[t]
        pr.n1[t,t] <- phi.native1[t]*p.native[t]
        pr.i2[t,t] <- phi.intro2[t]*p.intro[t]
        pr.n2[t,t] <- phi.native2[t]*p.native[t]
        # Above main diagonal
        for (j in (t+1):(n.occasions-1)){

            pr.i1[t,j] <- prod(phi.intro1[t:j])*prod(phi.intro2[(t+1):j])*prod(q.intro[t:(j-1)])*p.intro[j]
            pr.n1[t,j] <- prod(phi.native1[t:j])*prod(phi.native2[(t+1):j])*prod(q.native[t:(j-1)])*p.native[j]

            pr.i2[t,j] <- prod(phi.intro2[t:j])*prod(q.intro[t:(j-1)])*p.intro[j]
            pr.n2[t,j] <- prod(phi.native2[t:j])*prod(q.native[t:(j-1)])*p.native[j]
        } #j
        # Below main diagonal
        for (j in 1:(t-1)){
            pr.i1[t,j] <- 0
            pr.n1[t,j] <- 0
            pr.i2[t,j] <- 0
            pr.n2[t,j] <- 0
        } #j
    } #t
    # Last column: probability of non-recapture
    for (t in 1:(n.occasions-1)){
        pr.i1[t,n.occasions] <- 1-sum(pr.i1[t,1:(n.occasions-1)])
        pr.n1[t,n.occasions] <- 1-sum(pr.n1[t,1:(n.occasions-1)])
        pr.i2[t,n.occasions] <- 1-sum(pr.i2[t,1:(n.occasions-1)])
        pr.n2[t,n.occasions] <- 1-sum(pr.n2[t,1:(n.occasions-1)])
    } #t
    
    # Assess model fit using Freeman-Tukey statistic
    # Compute fit statistics for observed data
    for (t in 1:(n.occasions-1)){
        for (j in 1:n.occasions){
            expmarr.i1[t,j] <- r.i1[t]*pr.i1[t,j]
            expmarr.n1[t,j] <- r.n1[t]*pr.n1[t,j]
            expmarr.i2[t,j] <- r.i2[t]*pr.i2[t,j]
            expmarr.n2[t,j] <- r.n2[t]*pr.n2[t,j]
            E.org.i1[t,j] <- pow((pow(marr.i1[t,j], 0.5)-pow(expmarr.i1[t,j], 0.5)), 2)
            E.org.n1[t,j] <- pow((pow(marr.n1[t,j], 0.5)-pow(expmarr.n1[t,j], 0.5)), 2)
            E.org.i2[t,j] <- pow((pow(marr.i2[t,j], 0.5)-pow(expmarr.i2[t,j], 0.5)), 2)
            E.org.n2[t,j] <- pow((pow(marr.n2[t,j], 0.5)-pow(expmarr.n2[t,j], 0.5)), 2)
        }
    }
    # Generate replicate data and compute fit stats from them
    for (t in 1:(n.occasions-1)){
        marr.new.i1[t,1:n.occasions] ~ dmulti(pr.i1[t,], r.i1[t])
        marr.new.n1[t,1:n.occasions] ~ dmulti(pr.n1[t,], r.n1[t])
        marr.new.i2[t,1:n.occasions] ~ dmulti(pr.i2[t,], r.i2[t])
        marr.new.n2[t,1:n.occasions] ~ dmulti(pr.n2[t,], r.n2[t])
        for (j in 1:n.occasions){
            E.new.i1[t,j] <- pow((pow(marr.new.i1[t,j], 0.5)-pow(expmarr.i1[t,j], 0.5)), 2)
            E.new.n1[t,j] <- pow((pow(marr.new.n1[t,j], 0.5)-pow(expmarr.n1[t,j], 0.5)), 2)
            E.new.i2[t,j] <- pow((pow(marr.new.i2[t,j], 0.5)-pow(expmarr.i2[t,j], 0.5)), 2)
            E.new.n2[t,j] <- pow((pow(marr.new.n2[t,j], 0.5)-pow(expmarr.n2[t,j], 0.5)), 2)
        }
    }
    fit <- sum(E.org.i1[,]) + sum(E.org.n1[,]) + sum(E.org.i2[,]) + sum(E.org.n2[,]) 
    fit.new <-sum(E.new.i1[,]) + sum(E.new.n1[,]) + sum(E.new.i2[,]) + sum(E.new.n2[,])
}

